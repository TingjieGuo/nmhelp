<HTML><HEAD> <TITLE> bayes4.exa</TITLE><HEAD>
<BODY BGCOLOR="#FFFFFF">
<PRE>



 +--------------------------------------------------------------------+
 |                                                                    |
 |                          BAYES EXAMPLE 4                           |
 |                                                                    |
 +--------------------------------------------------------------------+

 This is example4.ctl from the NONMEM 7 distribution medium.  It, along
 with the data file, can be found in the examples directory.

 ;Model Desc: Population Mixture Problem in 1 Compartment model,
 ; with rate constant parameter and its inter-subject variances
 ; modeled as coming from two sub-populations
 ;Project Name: nm7examples
 ;Project ID: NO PROJECT DESCRIPTION

 $PROB RUN# example4 (from ad1tr1m2t)
 $INPUT C SET ID JID TIME CONC=DV DOSE=AMT RATE EVID MDV CMT VC1
        K101 VC2 K102 SIGZ PROB
 $DATA example4.csv IGNORE=C

 $SUBROUTINES ADVAN1 TRANS1

 $MIX
 P(1)=THETA(4)
 P(2)=1.0-THETA(4)
 NSPOP=2

 $PK
 MU_1=THETA(1)
 MU_2=THETA(2)
 MU_3=THETA(3)
 V=DEXP(MU_1+ETA(1))
 K10M=DEXP(MU_2+ETA(2))
 K10F=DEXP(MU_3+ETA(3))
 Q=1
 IF(MIXNUM.EQ.2) Q=0
 K=Q*K10M+(1.0-Q)*K10F
 S1=V

 $ERROR
 Y = F + F*EPS(1)

 $THETA
 (-1000.0  4.3 1000.0) ;[MU_1]
 (-1000.0 -2.9 1000.0) ;[MU_2]
 (-1000.0 -0.67 1000.0) ;[MU_3]
 (0.0001 0.667 0.9999)   ;[P(1)]

 $OMEGA BLOCK(3)
  .04  ;[p]
  0.01  ;[f]
  .027 ;[p]
  0.01  ;[f]
  0.001 ;[f]
  0.06 ;[p]

 $SIGMA
  0.01 ;[p]

 ; Prior information setup for OMEGAS only
 $PRIOR NWPRI

 ; Prior OMEGA
 $OMEGAP BLOCK(3)
  0.05 FIX
  0.0 0.05
  0.0 0.0  0.05

 ; Degrees of Freedom defined for Priors.
 $OMEGAPD (3 FIX)

 $EST METHOD=ITS INTERACTION NITER=30 PRINT=5 NOABORT SIGL=6
      FILE=example4.ext NOPRIOR=1 CTYPE=3 CITER=10 CALPHA=0.05

 $EST METHOD=IMP INTERACTION NITER=20 ISAMPLE=300 PRINT=1
      NOABORT SIGL=6 NOPRIOR=1

 $EST NBURN=500 NITER=500 METHOD=SAEM INTERACTION PRINT=10 SIGL=6
      ISAMPLE=2 NOPRIOR=1 MAPITER=0

 $EST METHOD=IMP INTERACTION EONLY=1 NITER=20 ISAMPLE=3000 PRINT=1
      NOABORT SIGL=6 NOPRIOR=1

 $EST METHOD=BAYES INTERACTION NBURN=2000 NITER=5000 PRINT=10
      FILE=example4.txt SIGL=6 NOPRIOR=0

 $EST MAXEVAL=9999 NSIG=3 SIGL=12 PRINT=1
      METHOD=CONDITIONAL INTERACTION
      NOABORT FILE=example4.ext NOPRIOR=1

 $COV MATRIX=R UNCONDITIONAL SIGL=10

REFERENCES: Guide <a href="intro7/index.html">Introduction_7<a/>

  <pre>Go to <A HREF=index.htm>main index</A>.</pre>
<pre>
  <hr ALIGN=LEFT WIDTH="450"></pre>
    <i><font size=-1>Created by nmhelp2html v. 1.0 written by Niclas Jonsson (Modified by AJB 5/2006,11/2007,10/2012)</font>
      </i><br>
</BODY></HTML>
