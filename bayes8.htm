<HTML><HEAD> <TITLE> bayes8.exa</TITLE><HEAD>
<BODY BGCOLOR="#FFFFFF">
<PRE>



 +--------------------------------------------------------------------+
 |                                                                    |
 |                          BAYES EXAMPLE 8                           |
 |                                                                    |
 +--------------------------------------------------------------------+

 This is example8.ctl from the NONMEM 7 distribution medium.  It, along
 with the data file, can be found in the examples directory.

 ;Model Desc: Two compartment Model, Using ADVAN3, TRANS4
 ;Project Name: nm7examples
 ;Project ID: NO PROJECT DESCRIPTION

 $PROB RUN# Example 8 (from samp5l)
 $INPUT C SET ID JID TIME  DV=CONC AMT=DOSE RATE EVID MDV CMT
        CLX V1X QX V2X SDIX SDSX
 $DATA example8.csv IGNORE=C

 $SUBROUTINES ADVAN3 TRANS4

 $PK
 include nonmem_reserved_general
 ; Request extra information for Bayesian analysis.
 ; An extra call will then be made for accepted samples
 BAYES_EXTRA_REQUEST=1
 MU_1=THETA(1)
 MU_2=THETA(2)
 MU_3=THETA(3)
 MU_4=THETA(4)
 CL=DEXP(MU_1+ETA(1))
 V1=DEXP(MU_2+ETA(2))
 Q=DEXP(MU_3+ETA(3))
 V2=DEXP(MU_4+ETA(4))
 S1=V1
 ; When Bayes_extra=1, then this particular set of individual
 ; parameters were "accepted" So you may record them if you wish
   IF(BAYES_EXTRA==1 .AND. ITER_REPORT&gt;=0 .AND. TIME==0.0) THEN
 "  WRITE(51,98) ITER_REPORT,ID,CL,V1,Q,V2
 " 98 FORMAT(I12,1X,F14.0,4(1X,1PG12.5))
 ENDIF

 $ERROR
 include nonmem_reserved_general
 BAYES_EXTRA_REQUEST=1
 Y = F + F*EPS(1)
 IF(BAYES_EXTRA==1 .AND. ITER_REPORT&gt;=0 ) THEN
 " WRITE(52,97) ITER_REPORT,ID,TIME,F
 " 97 FORMAT(I12,1X,F14.0,2(1X,1PG12.5))
 ENDIF

 ; Initial values of THETA
 $THETA
 (2.0) ;[LN(CL)]
 (2.0) ;[LN(V1)]
 (2.0) ;[LN(Q)]
 (2.0) ;[LN(V2)]
 ;INITIAL values of OMEGA
 $OMEGA BLOCK(4)
 0.15   ;[P]
 0.01  ;[F]
 0.15   ;[P]
 0.01  ;[F]
 0.01  ;[F]
 0.15   ;[P]
 0.01  ;[F]
 0.01  ;[F]
 0.01  ;[F]
 0.15   ;[P]
 ;Initial value of SIGMA
 $SIGMA
 (0.6 )   ;[P]

 $PRIOR NWPRI
 ; Prior information to the Thetas.
 $THETAP (2.0 FIX)x4
 $THETAPV BLOCK(4) FIX VALUES(10000.0,0.0)

 ; Prior information to the OMEGAS.
 $OMEGAP BLOCK(4)
 0.2 FIX
 0.0  0.2
 0.0  0.0 0.2
 0.0  0.0 0.0 0.2
 $OMEGAPD (4 FIX)

 $EST METHOD=BAYES INTERACTION FILE=example8.ext NBURN=10000
      NITER=1000 PRINT=100 NOPRIOR=0 CTYPE=3 CINTERVAL=100

REFERENCES: Guide <a href="intro7/index.html">Introduction_7<a/>

  <pre>Go to <A HREF=index.htm>main index</A>.</pre>
<pre>
  <hr ALIGN=LEFT WIDTH="450"></pre>
    <i><font size=-1>Created by nmhelp2html v. 1.0 written by Niclas Jonsson (Modified by AJB 5/2006,11/2007,10/2012)</font>
      </i><br>
</BODY></HTML>
