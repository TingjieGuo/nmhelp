<HTML><HEAD> <TITLE> tadexa.exa</TITLE><HEAD>
<BODY BGCOLOR="#FFFFFF">
<PRE>



 +--------------------------------------------------------------------+
 |                                                                    |
 |                   TIME AFTER DOSE (TAD) EXAMPLE                    |
 |                                                                    |
 +--------------------------------------------------------------------+

 This  fully-worked  out example shows how Time After Dose (TAD) may be
 computed in $PK abbreviated code.  It works with transient and steady-
 state doses, and also with additional and lagged doses.

 It is based on suggestions from the NONMEM tip directory

 https://nonmem.iconplc.com/nonmem/tips

 tip3-2-25-02.txt
 tip4-3-04-02.txt
 tip4update-10-11-13.txt

 but  is  not  identical to any of them.  See updates to these tips for
 nonmem 7.4.2.

 $PROB  Based on RUN# 705 nonmem coding challenge #1
 $INPUT C ID TIME DV AMT WT AGE CRCL SMK ADDL II EVID
 $DATA 704.csv IGNORE=C
 $SUBROUTINE ADVAN2 TRANS2
 $PK
 ; initialize for new individual or reset record
 IF (NEWIND.LT.2.OR.EVID.EQ.3) THEN
  TDOS=-999 ; Time of most recent dose. -999 if no previous dose.
  TAD=0     ; Time After Dose
 ENDIF
 ;
 ALAG1=0 ;  ALAG1 could be set to another value
 ;
 ; record TDOS (time of last dose) and compute TAD (time after dose)
 ; General case: ADDL doses and ALAG may be modelled
 IF (EVID.EQ.1.and.ALAG1.EQ.0.OR.EVID.EQ.4.and.ALAG1.EQ.0) TDOS=TIME
 IF (DOSTIM.GT.0) TDOS=DOSTIM
 IF (TDOS.GT.-999) TAD=TIME-TDOS

 ; If no ALAG or ADDL doses, the above three lines become:
 ;    IF (EVID.EQ.1.OR.EVID.EQ.4) TDOS=TIME
 ;    IF (TDOS.GT.-999) TAD=TIME-TDOS

   CL=THETA(1)*EXP(ETA(1))
   TVV=THETA(2)
   V=TVV*EXP(ETA(2))
   TVKA=THETA(3)
   KA=TVKA*EXP(ETA(3))
   S2=V
 $THETA
   (0, 10)
   (0, 100)
   (0, 0.5)
 $ERROR
   Y=F+ERR(1)
   IPRED=F
 $OMEGA
   0.04 ;[P] INTERIND VAR IN CL
   0.04 ;[P] INTERIND VAR IN V
   0.04 ;[P] INTERIND VAR IN KA
 $SIGMA
   0.2 ;[A] ADDITIVE COMPONENT
 $EST MAXEVAL=0 PRINT=2 NOABORT ;POSTHOC
 $TABLE ID TIME EVID TDOS TAD IPRED
        NOAPPEND NOPRINT ONEHEADER FILE=tadexa.tab
 FORMAT=SF11.7

 The data for the first subject is:

 C,Data Desc: test of TAD for ADDL,,,,,,,,,,
 C,ID,TIME,DV,AMT,WT,AGE,CRCL,SMK,ADDL,II,EVID
 ,1,0,0,0,58.4,51,4.49,1,0,0,2
 ,1,0.1,0,500,58.4,51,4.49,1,1,24,1
 ,1,1.1,1.67538,0,58.4,51,4.49,1,0,0,0
 ,1,4.1,2.79283,0,58.4,51,4.49,1,0,0,0
 ,1,10.1,0.583263,0,58.4,51,4.49,1,0,0,0
 ,1,24.1,0.146255,0,58.4,51,4.49,1,0,0,0
 ,1,25.1,1.67538,0,58.4,51,4.49,1,0,0,0

 The complete data set may be found in the tip directory.

 Note 1.
      It is assumed that TAD is displayed in tables for the purpose  of
      graphical display of the data.  It is not a random variables, and
      is not intended to be part of the prediction model.

 Note 2.
      If there are non-dose records prior to the first dose  record  of
      the  Individual  record,  TAD  is  set to zero for these records.
      These is an arbitrary conventions; the user could  use  different
      values,  especially  if these conditions might signal an error in
      the data set.

 Note 3.
      ALAG1 is used in the example, but a different dosing  compartment
      could  be  used,  e.g.,  ALAG2 for doses into compartment 2.  The
      code would need modification to compute TAD for doses into two or
      more compartments.

 Note 4.
      This  code  can  handle random DOSTIM (Eta on ALAG or ETA on mod-
      elled duration or rate.)

 Note 5.
      The code does not use INFN. The  information  is  developed  with
      passes  through  the  data  set at ICALL=2, in which NONMEM calls
      PREDPP.  If INFN is used, PREDPP is not called.  The  abbreviated
      code would have to account for additional and lagged dose times.

 Note 6.
      TDOS=-999  until the first dose. TDOS=0 when the first dose is at
      TIME 0.  This makes it possible to dispense with a flag  variable
      whose  only  function  is to say "a dose has occurred".  If there
      are negative time values in the data set and any  time  value  is
      smaller than -999, then

 Note 7.
      The code is intended to compute TAD for multiple individual doses
      or one initiating dose.  (An initiating dose has ADDL&gt;0,II&gt;0).

 REFERENCES: None


  <pre>Go to <A HREF=index.htm>main index</A>.</pre>
<pre>
  <hr ALIGN=LEFT WIDTH="450"></pre>
    <i><font size=-1>Created by nmhelp2html v. 1.0 written by Niclas Jonsson (Modified by AJB 5/2006,11/2007,10/2012)</font>
      </i><br>
</BODY></HTML>
