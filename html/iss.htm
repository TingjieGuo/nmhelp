<HTML><HEAD> <TITLE> iss.exa</TITLE><HEAD>
<BODY BGCOLOR="#FFFFFF">
<PRE>



 +--------------------------------------------------------------------+
 |                                                                    |
 |                    INITIAL STEADY STATE EXAMPLE                    |
 |                                                                    |
 +--------------------------------------------------------------------+

 This  is  an   example  of  the use of the Initial Steady State (I_SS)
 reserved variable in $PK.  It was provided by Nick Holford, Dept Phar-
 macology & Clinical Pharmacology, University of Auckland.

 Glucose  and  insulin  are  both endogenous substances, whose rates of
 formation are given by terms RGLU and RINS in the  differential  equa-
 tions.   In  the  absence of dose events, both glucose and insulin are
 assumed to be at steady state levels.  Exogenous doses of both glucose
 and insulin are also present in the first subject's data.

 It  is  possible  to  intialize the steady state condition with a dose
 record having AMT=0, RATE=0, SS=1, but this is artificial.   The  ini-
 tial condition  that the system is at steady state is a feature of the
 model, not of the data.  In this example, the $PK block  contains  the
 statement  I_SS=1.  This causes the system to be initialized appropri-
 ately.  The statement I_SS=1 could also have  been  placed  among  the
 $MODEL  statements.   However  the  initialization  is  performed, the
 result is the same: non-zero concentrations GLU and INS  are  computed
 at TIME=0.

 The SS data item need not be present in the data set for the I_SS com-
 putation. It is present in this example in order to  demonstrate  that
 SS  may also be computed using a dose record (see the third and fourth
 subjects' data).

 Note that SIGMA and OMEGA are fixed to 0 for purposes of illustration,
 so  that  the  simulated  values are exactly equal to those that arise
 from the model without the random variability  due  to  ETA  and  EPS.
 NONMEM issues warning messages ("INITIAL ESTIMATE OF WITHIN INDIVIDUAL
 VARIANCE IS ZER0").  These may be ignored.

 This example also illustrates the use of A(n)  on  the  right  in  the
 $ERROR  block  to obtain the compartment amounts that are used for the
 computation of concentrations GLU and INS.

 $PROB nicksiss6.ctl I_SS=1 in $PK ADVAN6
 $DATA Nicksiss.dat
 $INPUT ID TIME CMT AMT RATE SS DV
 $SIM (200070927) ONLYSIM NSUB=1

 ;Silber HE, Jauslin PM, Frey N, Gieschke R, Simonsson US, Karlsson MO.
 ;An integrated model for glucose and insulin regulation in healthy
 ;volunteers and type 2 diabetic patients following intravenous glucose
 ;provocations.  J Clin Pharmacol. 2007 Sep;47(9):1159-71.

 $THETA
 40 ; POP_RGLU MMOL/H/70KG - glucose input rate
 40 ; POP_VGLU L/70KG - volume of distribution for glucose
 5 ; POP_CLGLU L/H/70KG - glucose clearance
 1 ; POP_EMXGLU - EMAX for effect of glucose on insulin input
 10 ; POP_C50GLU MMOL/L - EC50 for effect of glucose on Insulin input
 5 ; POP_HILGLU - Hill parameter for effect of glucose on Insulin input

 5000 ; POP_RINS PMOL/H/70KG - insulin input rate
 5 ; POP_VINS L/70KG - volume of distribution for insulin
 70 ; POP_CLINS L/H/70KG - insulin clearance
 2 ; POP_EMXINS - EMAX for effect of insulin on glucose clearance
 50 ; POP_C50INS PMOL/L - EC50 for effect of insulin on glucose clearance
 2 ; POP_HILINS - Hill parameter for effect of insulin on glucose clearance

 $OMEGA
 0 FIX ;PPV_RGLU
 0 FIX ;PPV_RINS

 $SIGMA
 0 FIXED ;0.1  ;G_EXP_RUV
 0 FIXED ;1    ;G_ADD_RUV MMOL/L
 0 FIXED ;0.1  ;I_EXP_RUV
 0 FIXED ;1    ;I_ADD_RUV PMOL/L

 $SUBR ADVAN6 TOL=3

 $MODEL
    COMP (GLUCOSE)
    COMP (INSULIN)

 $PK
    I_SS=1

    ; GLUCOSE
    RGLU=THETA(1)*EXP(ETA(1))
    VGLU=THETA(2)
    CLGLU=THETA(3)
    EMXGLU=THETA(4)
    C50GLU=THETA(5)
    HILGLU=THETA(6)

    ;INSULIN
    RINS=THETA(7)*EXP(ETA(2))
    VINS=THETA(8)
    CLINS=THETA(9)
    EMXINS=THETA(10)
    C50INS=THETA(11)
    HILINS=THETA(12)

    S1=VGLU
    S2=VINS

 $DES
    DGLU=A(1)/VGLU
    DINS=A(2)/VINS
    DGLUH=DGLU**HILGLU
    DGEFF=EMXGLU*DGLUH/(C50GLU**HILGLU+DGLUH) ; effect of glucose on insulin
    DINSH=DINS**HILINS
    DIEFF=EMXINS*DINSH/(C50INS**HILINS+DINSH) ; effect of insulin on glucose
    DADT(1)=RGLU - CLGLU*(1+DIEFF)*DGLU       ; glucose
    DADT(2)=RINS*(1+DGEFF)- CLINS*DINS        ; insulin

 $ERROR
    GLU=A(1)/VGLU   ; glucose concentration
    INS=A(2)/VINS   ; insulin concentration
    IF (CMT.EQ.1)THEN
      Y=GLU*(1+ERR(1))+ERR(2)
    ENDIF

    IF (CMT.EQ.2)THEN
      Y=INS*(1+ERR(3))+ERR(4)
    ENDIF

 $TABLE ID TIME CMT AMT RATE SS GLU INS
  ONEHEADER NOPRINT FILE=nicksiss6.tab

 The data (Nicksiss.dat) follows.

 #ID,TIME,CMT,AMT,RATE,SS,DV
 1    0   1   0    0    0 .
 1    0   2   0    0    0 .
 1   .1   1   25   0    0 .
 1   1.0  2   10   0    0 .
 1    3   1   0    0    0 .
 1    3   2   0    0    0 .
 1  100   1   .    .    . .
 1  100   2   .    .    . .
 2    0   1   0    0    0 .
 2    0   2   0    0    0 .
 2  100   1   .    .    . .
 2  100   2   .    .    . .
 3    0   1   0    0    1 .
 3  100   1   .    .    . .
 3  100   2   .    .    . .
 4    0   1   0    0    1 .
 4  100   1   .    .    . .
 4  100   2   .    .    . .

 (See <A HREF="i.htm#initial_steady_state">Initial Steady State</A>,I_SS,ISSMOD)
 (See <A HREF="i.htm#i_ss">i_ss</A>, <A HREF="i.htm#initial_condition">initial_condition</A>).

 REFERENCES: none.


  <pre>Go to <A HREF=index.htm>main index</A>.</pre>
<pre>
  <hr ALIGN=LEFT WIDTH="450"></pre>
    <i><font size=-1>Created by nmhelp2html v. 1.0 written by Niclas Jonsson (Modified by AJB 5/2006,11/2007,10/2012)</font>
      </i><br>
</BODY></HTML>
